---
date created: 2023-05-17 10:08
date updated: 2023-05-17 21:46
---

## Directional Light 定向光源

#opengl/Directional_Light

当光源远离时，来自光源的光线接近于彼此平行。看起来所有的光线都来自同一个方向，无论物体和/或观看者在哪里。当光源被建模为无限远时，它被称为定向光，因为它的所有光线都有相同的方向；它与光源的位置无关。

定向光源的一个很好的例子是我们所知道的太阳。太阳离我们不是无限远，但它离我们太远了，以至于我们可以在照明计算中感知到它是无限远的。然后，来自太阳的所有光线都被建模为平行光线，如下图所示：

![](https://s2.loli.net/2023/05/16/ZDYIe1dJbujH8nM.png)

因为所有的光线都是平行的，所以每个物体与光源位置的关系并不重要，因为场景中每个物体的光方向是相同的。因为光的方向矢量保持不变，照明计算对于场景中的每个物体都是相似的。

我们可以通过设置这样一个光线的方向是矢量来代替光源位置，着色器的计算基本保持不变，这次我们直接使用光的方向向量，而不是使用光的位置向量来计算lightDir向量

```glsl
struct Light {
    // vec3 position; // no longer necessary when using directional lights.
    vec3 direction;
  
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};
[...]
void main()
{
  vec3 lightDir = normalize(-light.direction);
  [...]
}
```

注意此处我们 **先将光线的防线取负** ，到目前位置，**我们计算都是物体到光源的方向**，但是人们一般都是希望光线是指向物体的，因此我们将光线方向取反，现在方向是指向光源的。同时要主要规范化向量。

这个 `lightDir` 会在之后用来计算漫反射和镜面反射。

为了方便展示使用多个立方体

```c++
for(unsigned int i = 0; i < 10; i++)
{
    glm::mat4 model = glm::mat4(1.0f);
    model = glm::translate(model, cubePositions[i]);
    float angle = 20.0f * i;
    model = glm::rotate(model, glm::radians(angle), glm::vec3(1.0f, 0.3f, 0.5f));
    lightingShader.setMat4("model", model);

    glDrawArrays(GL_TRIANGLES, 0, 36);
}
```

还需要指定光的方向

```c++
lightingShader.setVec3("light.direction", -0.2f, -1.0f, -0.3f); 	    
```

> 一段时间以来，我们一直将光的位置和方向向量作为vec3进行传递，但有些人倾向于将所有向量定义为vec4。当将位置向量定义为向量4时，将w分量设置为1.0是很重要的，这样可以正确应用平移和投影。然而，当将方向向量定义为vec4时，我们不希望平移产生效果（因为它们只是表示方向，仅此而已），所以我们将w分量定义为0.0。
> 方向向量可以表示为：vec4（-0.2f，-1.0f，-0.3f，0.0f）。这也可以作为灯光类型的简单检查：你可以检查w分量是否等于1.0，看看我们现在有灯光的位置向量，如果w等于0.0，我们有灯光的方向向量；因此基于以下内容调整计算：

```glsl
if(lightVector.w == 0.0) // note: be careful for floating point errors
  // do directional light calculations
else if(lightVector.w == 1.0)
  // do light calculations using the light's position (as in previous chapters)  
```

> 有趣的事实：这实际上是旧的OpenGL（固定功能）如何确定光源是定向光源还是位置光源，并基于此调整其照明。

### 简单总结

```glsl
struct Light {
    //vec3 position;!!!!!!!!!!!!
    vec3 direction;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};
uniform Light light;

uniform Material material;


void main()
{
    //diffuse lighting + ambient --- 现在不需要自身发光直接使用纹理
    vec3 norm = normalize(Normal);
    //!!!!!!!!!!!!!!!!!!!!!!
    vec3 lightDir = normalize(-light.direction);
    //calculate 光照方向和法向量的夹角
    float diff = max(dot(norm, lightDir), 0.0); //dot 的结果是cos值
    vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, TexCoords));
    vec3 ambient = light.ambient * vec3(texture(material.diffuse, TexCoords));



    //specular lighting
    vec3 viewDir = normalize(viewPos - FragPos);//视角向量
    vec3 reflectDir = reflect(-lightDir, norm);  //光线反射向量

    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    vec3 specular = light.specular * spec * texture(material.specular, TexCoords).rgb;


    vec3 result = ambient + diffuse + specular;//光的颜色*物体本身的颜色
    FragColor = vec4(result, 1.0);//返回结果
}
```

## point light 点光源

#opengl/point_Light

平行光非常适合用于照亮整个场景的全局光，但我们通常也希望在整个场景中散射几个点光源。点光源是一种光源，在世界的某个地方具有给定的位置，可以向各个方向照明，光线会随着距离的推移而逐渐消失。

![](https://s2.loli.net/2023/05/17/jgOlxeYvr9aFIkb.png)

在前面的章节中，我们一直在使用一个简单的点光源。我们在给定的位置有一个光源，它可以将光从给定的光位置散射到各个方向。然而，我们定义的光源模拟光线永远不会褪色，因此看起来光源非常强大。在大多数3D应用程序中，我们希望模拟一个只照亮靠近光源的区域而不是整个场景的光源。

### Attenuation 衰减

为了在光线传播的距离上降低光的强度，通常称为衰减。一种在距离上降低光强度的方法是简单地使用线性方程。这样的方程将在一定距离内线性地降低光强度，从而确保一定距离处的物体亮度较低。然而，这样的线性函数看起来有点假。在现实世界中，光通常站在附近很亮，但光源的亮度在远处会迅速减弱；剩余的光强度然后随着距离的推移而缓慢地减小。因此，我们需要一个不同的方程来降低光的强度。

![](https://s2.loli.net/2023/05/17/UoWwqOhFEYsg5AT.png)

d：distance
三个可配置向量: constant $K_c$ , liner $K_l$ , quadratic $K_q$

- constant : 通常为1.0 ，确保分母始终小于1
- liner : 线性的降低光照强度。
- quadratic : 二次项与距离的象限相乘，并设置光源强度的二次递减。当距离很小时，二次项与线性项相比不那么显著，但随着距离的增长，二次项会变得更大。

由于二次项，光将主要以线性方式减少，直到距离变得足够大，使得二次项超过线性项，然后光强度将更快地减少。由此产生的效果是，在近距离时，光线非常强烈，但随着距离的推移，光线会迅速失去亮度，直到最终以更慢的速度失去亮度。下图显示了这种衰减在100的距离上的效果：

![](https://s2.loli.net/2023/05/17/rRPnSUuYl2ZsTKy.png)

### 选择正确的值

设置正确的值取决于许多因素：环境、灯光覆盖的距离、灯光类型等。在大多数情况下，这只是一个经验问题和适度的调整。下表显示了这些术语可以用来模拟覆盖特定半径（距离）的逼真（某种）光源的一些值。第一列指定灯光将使用给定的项覆盖的距离。这些值是大多数灯光的良好起点，这得益于Ogre3D的wiki：

![](https://s2.loli.net/2023/05/17/JoaNXCWAfQsSP72.png)

其中 constant 始终为1 ， liner的值随距离的增加而减小，同时 quardratic 更小。我们一般使用32到100上面的值。

### 衰减的实现

为了实现衰减，我们定义了三个变量，同时因为是点光源，因此我们还需要使用light的positon

```glsl
struct Light {
    vec3 position;  
  
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
	
    float constant;
    float linear;
    float quadratic;
}; 
```

之后我们就需要设置uniform的值

```c++
lightingShader.setFloat("light.constant",  1.0f);
lightingShader.setFloat("light.linear",    0.09f);
lightingShader.setFloat("light.quadratic", 0.032f);	 
```

在片段着色器中实现衰减相对简单:我们简单地根据方程计算衰减值，并将其与环境，漫反射和镜面分量相乘。

我们可以通过内置的length函数来计算距离

```glsl
float distance    = length(light.position - FragPos);
float attenuation = 1.0 / (light.constant + light.linear * distance + 
    		    light.quadratic * (distance * distance));    
```

然后我们通过将衰减值与环境色、漫反射色和高光色相乘，将衰减值包含在照明计算中。

我们可以单独保留环境组件，这样环境照明就不会随着距离的增加而减少，但是如果我们使用超过一个光源，所有的环境组件就会开始堆叠起来。在这种情况下，我们也想减弱环境照明。简单地使用最适合您环境的方法。

```glsl
ambient  *= attenuation; 
diffuse  *= attenuation;
specular *= attenuation;   
```

## spotlight 

#opengl/spotlight

聚光灯是一种位于环境中某处的光源，它不是向所有方向发射光线，而是只向特定方向发射光线。结果是，只有在聚光灯方向的一定半径内的物体被照亮，其他物体都保持黑暗。路灯或手电筒就是聚光灯的一个很好的例子。
OpenGL中的聚光灯由世界空间位置、方向和指定聚光灯半径的截止角表示。对于每个片段，我们计算片段是否在聚光灯的截止方向之间（因此在其圆锥体中），如果是，我们相应地照亮片段。下图让您了解聚光灯的工作原理：

![](https://s2.loli.net/2023/05/17/EKNwHOBQYfsGLiA.png)


- lightdir: 从像素指向光源的向量
- spotdir: 光源的方向
- Phi $\phi$: 指定聚光灯半径的截止角度。这个角度之外的一切都没有被聚光灯照亮。
- Theta $\theta$ : 方向在 lightdir 和 spotdir 中间，重视需要比 phi 小

我们需要做的就是计算点积 --- 得到cos值，--- 从而得到 phi 

### 手电筒


我们需要手电筒的位置，以及其方向 和 截至角度

```glsl
struct Light {
    vec3  position;
    vec3  direction;
    float cutOff;
    ...
};  
```

```c++
lightingShader.setVec3("light.position",  camera.Position);
lightingShader.setVec3("light.direction", camera.Front);
lightingShader.setFloat("light.cutOff",   glm::cos(glm::radians(12.5f)));
```

正如您所看到的，我们没有为截止值设置角度，而是根据角度计算余弦值，并将余弦结果传递给片段着色器。原因是在片段着色器中，我们计算LightDir和SpotDir向量之间的点积，点积返回余弦值，而不是角度；我们不能直接将角度与余弦值进行比较。为了在着色器中获得角度，我们必须计算点积结果的反余弦，这是一个昂贵的操作。因此，为了节省一些性能，我们预先计算给定截止角的余弦值，并将此结果传递给片段着色器。由于两个角度现在都表示为余弦，我们可以直接在它们之间进行比较，而无需昂贵的运算。

```glsl
float theta = dot(lightDir, normalize(-light.direction));
    
if(theta > light.cutOff) 
{       
  // do lighting calculations
}
else  // else, use ambient light so scene isn't completely dark outside the spotlight.
color = vec4(light.ambient * vec3(texture(material.diffuse, TexCoords)), 1.0);
```

我们首先计算lightDir矢量和求反方向矢量之间的点积（求反，因为我们希望矢量指向光源，而不是来自光源）。确保对所有相关向量进行归一化。

> 您可能想知道为什么在if保护符中使用>符号而不是<符号。不应该小于光线的截止值才能进入聚光灯内吗?这是对的，但是不要忘记角度值是用余弦值表示的，0度的角表示为余弦值1.0，而90度的角表示为余弦值0.0，就像你在这里看到的那样


### 总结

```glsl
struct Light {
    vec3  position;
    vec3  direction;
    float cutOff;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;

    float constant;
    float linear;
    float quadratic;
};
uniform Light light;

uniform Material material;


void main()
{

    vec3 lightDir = normalize(light.position - FragPos);//得到光照的向量

    float theta = dot(lightDir, normalize(-light.direction));
    //对比cos值
    if (theta > light.cutOff)//超过一定的值---正常计算
    {
        vec3 norm = normalize(Normal);

        //calculate 光照方向和法向量的夹角
        float diff = max(dot(norm, lightDir), 0.0); //dot 的结果是cos值
        vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, TexCoords));
        vec3 ambient = light.ambient * vec3(texture(material.diffuse, TexCoords));



        //specular lighting
        vec3 viewDir = normalize(viewPos - FragPos);//视角向量
        vec3 reflectDir = reflect(-lightDir, norm);  //光线反射向量

        float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
        vec3 specular = light.specular * spec * texture(material.specular, TexCoords).rgb;


        vec3 result = ambient + diffuse + specular;//光的颜色*物体本身的颜色
        FragColor = vec4(result, 1.0);//返回结果
    }
    else {
    //显示原本的纹理
        FragColor = vec4(light.ambient * texture(material.diffuse, TexCoords).rgb, 1.0);

    }

}
```

### 边缘平滑

为了创造一个边缘平滑的聚光灯的效果，我们想要模拟一个有内锥和外锥的聚光灯。我们可以将内锥体设置为上一节中定义的锥体，但我们也想要一个外锥体，使光线从内到外锥体的边缘逐渐变暗。

为了创建一个外锥，我们简单地定义另一个余弦值，表示聚光灯的方向矢量和外锥矢量之间的角度(等于它的半径)。然后，如果碎片在内外锥之间，它应该计算一个介于0.0和1.0之间的强度值。如果碎片在内锥内，则其强度等于1.0，如果碎片在外锥外，则强度等于0.0。

![](https://s2.loli.net/2023/05/17/D28r9hMiAS1qQO6.png)

$\epsilon$ 是 $\Phi$ 和 $\gamma$ cos值的差 即 $\phi-\gamma$。

![](https://s2.loli.net/2023/05/17/qML5dkcmYbtOoe1.png)

有了这些值，我们就不需要if else 了

```glsl
float theta     = dot(lightDir, normalize(-light.direction));
float epsilon   = light.cutOff - light.outerCutOff;
float intensity = clamp((theta - light.outerCutOff) / epsilon, 0.0, 1.0);    
...
// we'll leave ambient unaffected so we always have a little light.
diffuse  *= intensity;
specular *= intensity;
...
```


### 总结

```glsl
struct Light {
    vec3  position;
    vec3  direction;
    float cutOff;
    float outerCutOff;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;

    float constant;
    float linear;
    float quadratic;
};
uniform Light light;

uniform Material material;


void main()
{

    vec3 lightDir = normalize(light.position - FragPos);//得到光照的向量

	//计算角度
    float theta = dot(lightDir, normalize(-light.direction));
    float epsilon = light.cutOff - light.outerCutOff;
    float intensity = clamp((theta - light.outerCutOff) / epsilon, 0.0, 1.0);
	//clamp 夹紧
	
    vec3 norm = normalize(Normal);

    //calculate 光照方向和法向量的夹角
    float diff = max(dot(norm, lightDir), 0.0); //dot 的结果是cos值
    vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, TexCoords));
    vec3 ambient = light.ambient * vec3(texture(material.diffuse, TexCoords));



    //specular lighting
    vec3 viewDir = normalize(viewPos - FragPos);//视角向量
    vec3 reflectDir = reflect(-lightDir, norm);  //光线反射向量

    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    vec3 specular = light.specular * spec * texture(material.specular, TexCoords).rgb;

    diffuse *= intensity;
    specular *= intensity;

    vec3 result = ambient + diffuse + specular;//光的颜色*物体本身的颜色
    FragColor = vec4(result, 1.0);//返回结果

}
```