# Material

#opengl/item_material

在现实生活中不同材质的反光性能也不一样，因此如果我们想要渲染多种物体，我们需要为每种表面定义**材质**(material)

在前一章中，我们定义了一个对象和光线颜色来定义对象的视觉输出，并结合了环境强度和镜面强度组件。

当描述一个表面时，我们可以为3个照明组件定义一个材质颜色:环境照明、漫射照明和高光照明。

通过为每个组件指定一种颜色，我们可以对曲面的颜色输出进行细粒度控制。现在，为这三种颜色添加一个反光组件，我们就拥有了所需的所有材质财产：

```glsl
#version 330 core
struct Material {
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    float shininess;
}; 
  
uniform Material material;
```

在片段着色器中，我们创建了一个结构来存储曲面的材质财产。我们也可以将它们存储为单独的uniform，但将它们存储在结构中可以使其更有组织性。我们首先定义结构的布局，然后简单地声明一个以新创建的结构为类型的统一变量。

我们为每个冯氏照明元素设置了一个颜色值。Lastly, the shininess impacts the scattering/radius of the specular highlight.通过这四种元素，我们可以模仿很多显示生活中的物体材质。

## setting materials

因为我们设置了一个uniform值，接下来我们需要使用material改变光线的计算方式。

```glsl
void main()
{    
    // ambient
    vec3 ambient = lightColor * material.ambient;
  	
    // diffuse 
    vec3 norm = normalize(Normal);
    vec3 lightDir = normalize(lightPos - FragPos);
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = lightColor * (diff * material.diffuse);
    
    // specular
    vec3 viewDir = normalize(viewPos - FragPos);
    vec3 reflectDir = reflect(-lightDir, norm);  
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    vec3 specular = lightColor * (spec * material.specular);  
        
    vec3 result = ambient + diffuse + specular;
    FragColor = vec4(result, 1.0);
}
```

我们将物体材质的所有值和对应的反射相乘。

然而，在设置制服时，GLSL中的结构在任何方面都不是特别的；结构体实际上只是作为统一变量的命名空间。如果我们想填充结构，我们必须设置单独的统一，但以结构的名称为前缀：

```c++
lightingShader.setVec3("material.ambient", 1.0f, 0.5f, 0.31f);
lightingShader.setVec3("material.diffuse", 1.0f, 0.5f, 0.31f);
lightingShader.setVec3("material.specular", 0.5f, 0.5f, 0.5f);
lightingShader.setFloat("material.shininess", 32.0f);
```

我们将环境和漫反射组件设置为我们想要的对象的颜色，并将对象的高光组件设置为中等明亮的颜色;我们不希望高光分量太强。我们也将亮度保持在32。

## lighting properties

#opengl/light_material

当前的阳光太亮了，原因是所有光源都全力反射，我们需要添加不同的强度。在上一节中我们通过添加强度选项来控制强弱。但是这次我们通过指定向量来控制强弱。

```glsl
vec3 ambient  = vec3(1.0) * material.ambient;
vec3 diffuse  = vec3(1.0) * (diff * material.diffuse);
vec3 specular = vec3(1.0) * (spec * material.specular); 
```

因此，对象的每个材质属性都会以灯光的每个组件的全部强度返回。现在，对象的环境光组件正在完全影响立方体的颜色。环境光组件实际上不应该对最终颜色产生如此大的影响，因此我们可以通过将灯光的环境光强度设置为较低的值来限制环境光颜色：

```glsl
vec3 ambient = vec3(0.1) * material.ambient;  
```

和之前一节一样，我们通过创造一个结构体，来存储光源的性质

```glsl
struct Light {
    vec3 position;
  
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

uniform Light light;  
```

ambient 一般会设置的很低。光源的漫反射成分通常被设置为我们想要的颜色;通常是明亮的白色。镜面组件通常保持在vec3(1.0)，在全强度下闪耀。注意，我们还将光的位置向量添加到结构中。

```glsl
vec3 ambient  = light.ambient * material.ambient;
vec3 diffuse  = light.diffuse * (diff * material.diffuse);
vec3 specular = light.specular * (spec * material.specular);  
```

We then want to set the light intensities in the application:

```glsl
lightingShader.setVec3("light.ambient",  0.2f, 0.2f, 0.2f);
lightingShader.setVec3("light.diffuse",  0.5f, 0.5f, 0.5f); // darken diffuse light a bit
lightingShader.setVec3("light.specular", 1.0f, 1.0f, 1.0f); 
```

正如您所看到的，不同的灯光颜色会极大地影响对象的颜色输出。由于灯光颜色直接影响对象可以反射的颜色（您可能还记得“颜色”一章中的内容），因此它对视觉输出有很大影响。

```c++
glm::vec3 lightColor;
lightColor.x = sin(glfwGetTime() * 2.0f);
lightColor.y = sin(glfwGetTime() * 0.7f);
lightColor.z = sin(glfwGetTime() * 1.3f);
  
glm::vec3 diffuseColor = lightColor   * glm::vec3(0.5f); 
glm::vec3 ambientColor = diffuseColor * glm::vec3(0.2f); 
  
lightingShader.setVec3("light.ambient", ambientColor);
lightingShader.setVec3("light.diffuse", diffuseColor);
```

## 总结

```glsl
#version 330 core
out vec4 FragColor;

//对应输入
in vec3 Normal;
in vec3 FragPos;


//镜像反射
uniform vec3 viewPos;

//定义不同的材质
struct Material {
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    float shininess;
};

struct Light {
    vec3 position;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};
uniform Light light;

uniform Material material;

//使用材质的值代替直接使用uniform
void main()
{
    //环境光 --- 0.1
    vec3 ambient = light.ambient * material.ambient;//环境光下的颜色

    //diffuse lighting
    vec3 norm = normalize(Normal);
    vec3 lightDir = normalize(light.position - FragPos);//得到光照的向量
    //calculate 光照方向和法向量的夹角
    float diff = max(dot(norm, lightDir), 0.0); //dot 的结果是cos值
    vec3 diffuse = light.diffuse * (diff * material.diffuse);//光照的颜色

    //specular lighting
    vec3 viewDir = normalize(viewPos - FragPos);//视角向量
    vec3 reflectDir = reflect(-lightDir, norm);  //光线反射向量

    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    vec3 specular = light.specular * (spec * material.specular);


    vec3 result = ambient + diffuse + specular;//光的颜色*物体本身的颜色
    FragColor = vec4(result, 1.0);//返回结果
}
```


uniform 值传递

```c++
while (!glfwWindowShouldClose(window))
{
	// per-frame time logic
	// --------------------
	float currentFrame = static_cast<float>(glfwGetTime());
	deltaTime = currentFrame - lastFrame;
	lastFrame = currentFrame;

	// input
	// -----
	processInput(window);

	// render
	// ------
	glClearColor(0.1f, 0.1f, 0.1f, 1.0f);
	glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);

	// be sure to activate shader when setting uniforms/drawing objects
	lightingShader.use();
	//# Material
	lightingShader.setVec3("material.ambient", 1.0f, 0.5f, 0.31f);
	lightingShader.setVec3("material.diffuse", 1.0f, 0.5f, 0.31f);
	lightingShader.setVec3("material.specular", 0.5f, 0.5f, 0.5f);
	lightingShader.setFloat("material.shininess", 32.0f);
	//light
	lightingShader.setVec3("light.ambient", 0.2f, 0.2f, 0.2f);
	lightingShader.setVec3("light.diffuse", 0.5f, 0.5f, 0.5f); // darken diffuse light a bit
	lightingShader.setVec3("light.specular", 1.0f, 1.0f, 1.0f);
	lightingShader.setVec3("light.position", lightPos);
	lightingShader.setVec3("viewPos", camera.Position);

	// view/projection transformations
	glm::mat4 projection = glm::perspective(glm::radians(camera.Zoom), (float)SCR_WIDTH / (float)SCR_HEIGHT, 0.1f,
											100.0f);
	glm::mat4 view = camera.GetViewMatrix();
	lightingShader.setMat4("projection", projection);
	lightingShader.setMat4("view", view);


	// world transformation
	glm::mat4 model = glm::mat4(1.0f);
	lightingShader.setMat4("model", model);

	// render the cube
	glBindVertexArray(cubeVAO);
	glDrawArrays(GL_TRIANGLES, 0, 36);


	// also draw the lamp object
	lightCubeShader.use();
	lightCubeShader.setMat4("projection", projection);
	lightCubeShader.setMat4("view", view);
	model = glm::mat4(1.0f);
	model = glm::translate(model, lightPos);
	model = glm::scale(model, glm::vec3(0.2f)); // a smaller cube
	lightCubeShader.setMat4("model", model);

	glBindVertexArray(lightCubeVAO);
	glDrawArrays(GL_TRIANGLES, 0, 36);


	// glfw: swap buffers and poll IO events (keys pressed/released, mouse moved etc.)
	// -------------------------------------------------------------------------------
	glfwSwapBuffers(window);
	glfwPollEvents();
}
```